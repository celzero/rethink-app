plugins {
    // to download blocklists for the headless variant
    id "de.undercouch.download" version "5.3.0"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ex) {
    logger.warn("No keys.  Release disabled: $ex.message")
    keystoreProperties['keyAlias'] = ''
    keystoreProperties['keyPassword'] = ''
    keystoreProperties['storeFile'] = '/dev/null'
    keystoreProperties['storePassword'] = ''
}

// stackoverflow.com/a/28661942
// stackoverflow.com/a/22743664
tasks.whenTaskAdded { t ->
    if (t.name.toLowerCase().contains('headless')) {
        // this task maybe added more than once, but downloads happen only once
        t.dependsOn downloadBlocklists
    }
}

android {
    compileSdkVersion 33
    // https://developer.android.com/studio/build/configure-app-module
    namespace 'com.celzero.bravedns'

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    defaultConfig {
        applicationId "com.celzero.bravedns"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 25 // for version name 053n
        versionName "053n"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            // modified as part of #352, now webview is removed from app, flipping back
            // the setting to true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        leakCanary {
            matchingFallbacks = ['debug']
            initWith buildTypes.debug
        }
    }
    flavorDimensions "releaseChannel", "releaseType"
    productFlavors {
        play {
            dimension "releaseChannel"
        }
        fdroid {
            dimension "releaseChannel"
        }
        website {
            dimension "releaseChannel"
        }
        headless {
            dimension "releaseType"
            // stackoverflow.com/a/60560178
            // buildConfigField 'string', 'timestamp', '1662384683026'
        }
        full {
            dimension "releaseType"
        }
    }

    buildFeatures {
        viewBinding true
    }

    kapt {
        correctErrorTypes true
    }

}

dependencies {
    androidTestImplementation 'androidx.test:rules:1.5.0'
    def room_version = "2.5.0"
    def paging_version = "3.1.1"

    implementation "com.google.guava:guava:31.1-jre"

    // https://developer.android.com/studio/write/java8-support
    // included to fix issues with Android 6 support, issue#563
    // new version 2.0.0 need minimum of gradle plugin 7.4.0-alpha10
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.2.0")

    fullImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    fullImplementation 'androidx.appcompat:appcompat:1.7.0-alpha01'
    fullImplementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    fullImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    fullImplementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    fullImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    fullImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // For liveData implementation
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    fullImplementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    fullImplementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    fullImplementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'

    // For paging - connection tracker
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    fullImplementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'com.google.android.material:material:1.8.0'
    fullImplementation 'androidx.viewpager2:viewpager2:1.0.0'

    fullImplementation 'com.squareup.okhttp3:okhttp:4.10.0'
    fullImplementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.10.0'

    fullImplementation 'com.squareup.retrofit2:retrofit:2.9.0'
    fullImplementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Glide implementation
    implementation('com.github.bumptech.glide:glide:4.14.2') {
        exclude group: 'glide-parent'
    }
    // Ref - https://stackoverflow.com/questions/46638056/how-to-use-glidemodule-on-glide-4/46638213#46638213
    kapt 'com.github.bumptech.glide:compiler:4.14.2'

    // Swipe button animation
    fullImplementation 'com.facebook.shimmer:shimmer:0.5.0'

    // Koin core features
    implementation "io.insert-koin:koin-core:3.3.3"
    // Koin main features for Android (Scope,ViewModel ...)
    implementation "io.insert-koin:koin-android:3.2.3"

    implementation 'hu.autsoft:krate:2.0.0'

    // To use only without reflection variants of viewBinding
    fullImplementation 'com.github.kirich1409:viewbindingpropertydelegate:1.5.6'
    fullImplementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.6'

    // Tun2socks (https://jitpack.io/#celzero/firestack)
    implementation 'com.github.celzero:firestack:4bda1893c7'
    // implementation project(":tun2socks")

    // Work manager
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    // for handling IP addresses and subnets, both IPv4 and IPv6
    // https://seancfoley.github.io/IPAddress/ipaddress.html
    implementation 'com.github.seancfoley:ipaddress:5.4.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    leakCanaryImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    fullImplementation 'androidx.biometric:biometric:1.1.0'

    playImplementation 'com.google.android.play:core:1.10.3' // for new version updater
}

// github.com/michel-kraemer/gradle-download-task/issues/131#issuecomment-464476903
task downloadBlocklists(type: Download) {
    // def assetsDir = new File(projectDir, 'src/main/assets'
    def assetsDir = android.sourceSets.headless.assets.srcDirs[0]
    // the filenames are ignored by dl, but acts as a hint for the output
    // filename for the download-plugin, which does not respect the
    // content-disposition http header, but rather guesses dest file names
    // from the final segment of url's path
    // github.com/michel-kraemer/gradle-download-task/blob/64d1ce32/src/main/java/de/undercouch/gradle/tasks/download/DownloadAction.java#L731
    def sources = [
            'https://dl.rethinkdns.com/blocklists/filetag.json',
            'https://dl.rethinkdns.com/basicconfig/basicconfig.json',
            'https://dl.rethinkdns.com/rank/rd.txt',
            'https://dl.rethinkdns.com/trie/td.txt',
    ]
    src(sources)
    dest assetsDir
    // download files only if last-modified of the local file is less than
    // the last-modified http header returned by the server
    onlyIfModified true
    // or if etag mismatches
    useETag true
    // overwrite older files as determined by last-modified, always
    overwrite true
}
